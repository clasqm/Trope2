/////////////Main Message Loop//////////////////
//This main loop was getting too large for comfort and has been 
//split up into a number of subroutines
while(not leavingLoop)
	sleep 0.01
	nCommands = token(message$, msg$(), "|")
	for everyCommand = 1 to nCommands
		if(DEBUG and msg$(everyCommand)<>"") print msg$(everyCommand)
		TropeFileLoop(msg$(everyCommand))
		TropeEditLoop(msg$(everyCommand))
		TropeSettingsLoop(msg$(everyCommand))
		TropeHelpLoop(msg$(everyCommand))
		if AppName$ = "Rondel" RondelLoop(msg$(everyCommand))
		switch(msg$(everyCommand))
		///////////////////QUIT///////////////////
			case "_QuitRequested":
			case "MainWindow:_QuitRequested":
			case "MenuView:File:Quit":
			case "QuitButton":
				leavingLoop = true
				break
			case "MainWindow:Activated":
			//check if another instance has changed the settings
			GetSettings()
			break
		
		/////////////RECENT//////////////
			case "MenuView:Recent...:Clear Recent Files":
				DeleteRecent()
				for f = 1 to 10
					RecentFile$(f) =""
				next f
				Menu set "Recent...","Clear Recent Files", "disable", "MenuView"
				break
						
		////////StatusButton//////////////
			case "StatusButton":
				RefreshStatusbar()
				break
		///////PasteSnippetButton////////
			case "PasteSnippetButton"
				PasteSnippet()
				break
		//////////////////////////////////
			default:
				break
		end switch

		//Check if file has been dropped
		if left$(msg$(everyCommand), 20) = "MainWindow:_Dropped:" then
			DroppedFile$ = mid$(msg$(everyCommand), 21)
			if FileIsOpen = 0 then
				OpenFile(DroppedFile$)
				RegenerateRecent()
				if CurrentFile$ <> "" Menu set "File", "Revert", "enable", "MenuView"
			end if
		end if
		
		// Check if user has requested a recent file
		if left$(msg$(everyCommand), 19) = "MenuView:Recent...:" and mid$(msg$(everyCommand),20) <> "Clear Recent Files" then
			//if TextFileCheck(mid$(msg$(everyCommand),20)) = 1 break
			OpenFile(mid$(msg$(everyCommand),20))
			FileIsOpen = 1
			RegenerateRecent()
			if CurrentFile$ <> "" Menu set "File", "Revert", "enable", "MenuView"
		end if

		//check if user wants to insert a high-ASCII character
		if left$(msg$(everyCommand), 14) = "MenuView:Ascii" then
			ReplaceChar(msg$(everyCommand))
			FileIsOpen = 1
		end if
		
		//check if user wants to insert a Unicode character
		if left$(msg$(everyCommand), 16) = "MenuView:Unicode" then
			ReplaceChar(msg$(everyCommand))
			FileIsOpen = 1
		end if
		
		//check if user selected a snippet
		if left$(msg$(everyCommand), 15) = "SnippetDropBox:" Snippet$ = mid$(msg$(everyCommand), 16)
		
		//check if user requestes lorem ipsum text
		if left$(msg$(everyCommand), 30) = "MenuView:Help:Lorem Ipsum ...:" then
			InsertLoremIpsum(msg$(everyCommand))
		endif
		
		//Check if user selected a language
		if left$(msg$(everyCommand),27)  = "MenuView:Settings:Language:" then
			Language$ = right$(msg$(everyCommand), len(msg$(everyCommand))-27)
			If Language$ = "None" then 
				Language$ = ""
				submenu set "Settings", "Autosuggest", "Assist OFF", "disable", "MenuView"
				submenu set "Settings", "Autosuggest", "Assist ON", "disable", "MenuView"
				submenu set "Settings", "Autosuggest", "Edit User's word list", "disable", "MenuView"
				textedit set "EditBox", "has-autocompletion", 0
			else
				AddSpellAssistTerms()				
			endif
			ChangeTitleBar()
		endif

		//Check if user selected to install a language
		if left$(msg$(everyCommand),26)  = "MenuView:Settings:Install:" then
			Language$ = right$(msg$(everyCommand), len(msg$(everyCommand))-26)
			system("/boot/system/apps/Terminal pkgman install aspell_" + GetAspellAbbr$())
			Language$ = ""
			alert "A restart is required before the new language will be selectable", "Info", "Ok"
			ChangeTitleBar()
		endif
		
		RefreshStatusbar()

	next everyCommand

	//Check if user has just entered text without doing anything else. Like saving.
//	if textedit get "EditBox", "textlength" > 0 FileIsOpen = 1
	if TextCount <> textedit get "EditBox","textlength" then 
		FileIsOpen = 1
		if abs(TextCount - textedit get "EditBox","textlength") >30 then
			Saved$ = "unsaved"
			ChangeTitleBar()
			TextCount = TextCount + 30
		endif 
	else
		FileIsOpen = 0
		Saved$ = "saved"
	endif
wend
CloseWindow(0)
end

